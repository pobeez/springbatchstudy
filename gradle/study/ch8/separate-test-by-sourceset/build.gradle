apply plugin: 'java'

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
}

sourceSets {
	integrationTest {
	    java.srcDir file('src/integrationTest/java')
	    compileClasspath = sourceSets.main.output + configurations.testCompile
	    runtimeClasspath = compileClasspath + output
	}
}

test {
	reports.html.destination = file("${reports.html.destination}/unit")
	reports.junitXml.destination = file("${reports.junitXml.destination}/unit")
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    
    // for report
    reports.html.destination = file("${reports.html.destination}/integration")
    reports.junitXml.destination = file("${reports.junitXml.destination}/integration")
}
